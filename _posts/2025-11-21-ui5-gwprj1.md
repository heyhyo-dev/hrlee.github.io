---
title: "[UI5] Gateway Service로 항공사 리포트 구현하기"
excerpt: "SAP Gateway Project1"

categories:
  - UI5
tags:
  - [UI5]

permalink: /ui5/gwprj1/

toc: true
toc_sticky: true

date: 2025-11-21
last_modified_at: 2025-11-21
---

<img width="1536" height="1024" alt="image" src="https://github.com/user-attachments/assets/4c4339a3-ae0a-49ea-8046-dd754b9f00e9" />


> **메인 프로세스 전개 과정**   
> 1. Gateway Service를 생성한다.
> 2. Data Model 생성 후 MPC(Model Provider Class)의 메소드를 재정의한다.
> 3. BAS에서 view 로직 추가하여 실행한다.


---

## Gateway Service 생성하기 <mark>SEGW</mark>
<img width="928" height="536" alt="image" src="https://github.com/user-attachments/assets/8d294033-5c8b-4c09-a6bd-12a875572689" />  

<img width="562" height="227" alt="image" src="https://github.com/user-attachments/assets/613f58f9-a7f0-4f57-88d1-6268eaf02881" />  

+ Data Model   
  - Entity Type : 테이블의 구조(스키마)에 해당하는 오브젝트로, 필드(속성, Property)들의 집합이다.  
  <img width="1754" height="195" alt="image" src="https://github.com/user-attachments/assets/7fac5b64-72ca-4f56-9cf4-27791f32d06d" />

  - Associations : 두 Entity Type 간의 관계(Relationship). DB에서 FK(외래키) 관계와 유사한 개념이다.
    
  - Entity Set : Entity Type 의 컬렉션(Collection). 하나의 Entity Type 은 하나의 Entity Set 에 매핑된다. 입력/조회/수정/삭제/검색 기능이 적용된다.  
  <img width="1256" height="190" alt="image" src="https://github.com/user-attachments/assets/bdca6d89-686a-485c-a2e6-be83cfbf91f6" />  

---

### Entity Type 생성
SAP SFLIGHT Table 기반으로 생성해보자.  
<img width="977" height="414" alt="image" src="https://github.com/user-attachments/assets/8ef28337-c12e-4bcf-9535-8d0abf6516a5" />  

<img width="1229" height="669" alt="image" src="https://github.com/user-attachments/assets/e1205863-1cce-48fb-893c-a9dc18bcb250" />  
MANDT 제외한 필드 선택, Serach Help도 선택 가능하다.  

<img width="1229" height="673" alt="image" src="https://github.com/user-attachments/assets/43a30429-67d3-46a2-9338-6cdf623292cd" />
키 필드를 체크하여 설정한다.  

<img width="1276" height="471" alt="image" src="https://github.com/user-attachments/assets/0465830f-69a8-407a-ab35-cace1a2b707a" />  
**Entity Type, Entity Set, Service Implementation 이 생성된다.**  

<img width="1810" height="462" alt="image" src="https://github.com/user-attachments/assets/75f667f6-6027-4e03-a69a-2d5b7ced558c" />  
* Scale : decimal type의 소수점 아래 자릿수  
* Unit Property Name : 금액처럼 통화가 필요한 경우 Reference할 Property를 지정함. (SFLIGHT에서 이미 지정되어 있어서 따로 지정하지 않는다.)  

<img width="1860" height="336" alt="image" src="https://github.com/user-attachments/assets/8ddcc09f-0893-4c4a-bac8-5c6151f10579" />  
Entity Sets의 SflightEntitySet 더블클릭하여 entityset에 생성/갱신/삭제 체크박스 표시한다.  

generate 버튼 클릭하여 활성화한다.  

> 그러나 바로 사용할 수 없다. CRUD 로직을 넣어야 사용 가능하다.

---

## OData 서비스 생성/활성화하기
<img width="1020" height="447" alt="image" src="https://github.com/user-attachments/assets/3bd71d64-ed5b-4941-97b9-6b9d70db31fe" />  
<img width="715" height="163" alt="image" src="https://github.com/user-attachments/assets/1a9fb830-c997-4bd9-ada8-55bd8ff49ece" />  
/n/IWFND/MAINT_SERVICE 와 같은 행위다.  


## Data Model 생성 - Data Provider Class
<mark>SEGW</mark>  
<img width="663" height="377" alt="image" src="https://github.com/user-attachments/assets/558028af-12ac-4cbc-a890-cd059733b496" />  
이제 CRUD-Q 로직을 구현하기 위해 더블클릭하여 Class로 이동한다.  

<img width="969" height="350" alt="image" src="https://github.com/user-attachments/assets/342f5f63-b881-4434-97d7-ddd214533e90" />  
*재정의된 건 검은색으로 표시된다.*  
SflightEntitySet처럼 Free Style Modeling으로 만들어진 Entity Set의 경우에는 Method 안에 Exception 처리 로직만 있다.  
따라서, Data Provider Extension Class에서 관련 Method를 Redefine한 후 직접 데이터 컨트롤 로직을 구현해야 한다.  

<details>
<summary><mark>SFLIGHTENTITYSET_CREATE_ENTITY</mark> 1개의 Entry를 전달받아 신규 데이터를 생성하는 Method. </summary>
<div markdown="1">
```abap
  METHOD SFLIGHTENTITYSET_CREATE_ENTITY.
    DATA : LS_REQUEST_INPUT_DATA TYPE YCL_YGWSFLIGHT2_MPC_EXT=>TS_SFLIGHTENTITY.
    DATA : LS_DATA TYPE SFLIGHT.

    "전달받은 입력 대상 데이터 추출
    IO_DATA_PROVIDER->READ_ENTRY_DATA( IMPORTING ES_DATA = LS_REQUEST_INPUT_DATA ).

    "저장 데이터 구조로 변환
    LS_DATA = CORRESPONDING #( LS_REQUEST_INPUT_DATA ).

    "데이터 저장
    INSERT SFLIGHT FROM LS_DATA.

    "저장 성공 데이터 반환
    ER_ENTITY = CORRESPONDING #( LS_DATA ).

  ENDMETHOD.
```  
</div>
</details>


<details>
<summary><mark>SFLIGHTENTITYSET_DELETE_ENTITY</mark> Key Property에 따라 1개 Entity를 삭제하는 Method. </summary>
<div markdown="1">
```abap
  METHOD SFLIGHTENTITYSET_DELETE_ENTITY.
    DATA : LS_DATA TYPE SFLIGHT.

    LOOP AT IT_KEY_TAB INTO DATA(LS_KEY).
      CASE LS_KEY-NAME. "전달받은 Key Property명. 대소문자 구분
        WHEN 'Carrid'.
          LS_DATA-CARRID = LS_KEY-VALUE.
        WHEN 'Connid'.
          LS_DATA-CONNID = LS_KEY-VALUE.
        WHEN 'Fldate'.
          LS_DATA-FLDATE = LS_KEY-VALUE.
      ENDCASE.
    ENDLOOP.

    "데이터 삭제
    DELETE SFLIGHT FROM LS_DATA.

  ENDMETHOD.
```  
</div>
</details>


<details>
<summary><mark>SFLIGHTENTITYSET_GET_ENTITY</mark> Key Property에 따라 1개의 Entry만 반환하는 Method. </summary>
<div markdown="1">
```abap
  METHOD SFLIGHTENTITYSET_GET_ENTITY.
    DATA : LS_DATA TYPE SFLIGHT.

    LOOP AT IT_KEY_TAB INTO DATA(LS_KEY).
      CASE LS_KEY-NAME. "Property명. 대소문자 구분
        WHEN 'Carrid'.
          LS_DATA-CARRID = LS_KEY-VALUE.
        WHEN 'Connid'.
          LS_DATA-CONNID = LS_KEY-VALUE.
        WHEN 'Fldate'.
          LS_DATA-FLDATE = LS_KEY-VALUE.
      ENDCASE.
    ENDLOOP.

    "key값에 맞는 data 1건 검색
    SELECT SINGLE * INTO @LS_DATA
      FROM SFLIGHT
     WHERE CARRID = @LS_DATA-CARRID
       AND CONNID = @LS_DATA-CONNID
       AND FLDATE = @LS_DATA-FLDATE.

    CHECK SY-SUBRC = 0.

    ER_ENTITY = CORRESPONDING #( LS_DATA ). "데이터 반환
  ENDMETHOD.
```  
</div>
</details>

<details>
<summary><mark>SFLIGHTENTITYSET_GET_ENTITYSET</mark> Filter값에 따른 1개 이상의 Entry들을 반환하는 Method. </summary>
<div markdown="1">
```abap
METHOD SFLIGHTENTITYSET_GET_ENTITYSET.
    DATA : LT_ENTITYSET TYPE YCL_YGWSFLIGHT_MPC=>TT_SFLIGHTENTITY,
           LS_ENTITY    TYPE YCL_YGWSFLIGHT_MPC=>TS_SFLIGHTENTITY,
           LR_CARRID    TYPE RANGE OF YCL_YGWSFLIGHT_MPC=>TS_SFLIGHTENTITY-CARRID,
           LR_CONNID    TYPE RANGE OF YCL_YGWSFLIGHT_MPC=>TS_SFLIGHTENTITY-CONNID,
           LR_FLDATE    TYPE RANGE OF YCL_YGWSFLIGHT_MPC=>TS_SFLIGHTENTITY-FLDATE.

    DATA(LO_FILTER) = IO_TECH_REQUEST_CONTEXT->GET_FILTER( ).
    DATA(LT_FILTER_SELECT_OPTIONS) = LO_FILTER->GET_FILTER_SELECT_OPTIONS( ).

"검색속도 향상을 위한 최대 조회건수
    DATA(LV_TOP) = IO_TECH_REQUEST_CONTEXT->GET_TOP( ) + IS_PAGING-SKIP.

    LOOP AT LT_FILTER_SELECT_OPTIONS INTO DATA(LS_FILTER).
      CASE LS_FILTER-PROPERTY.
        WHEN 'CARRID'.
          LO_FILTER->CONVERT_SELECT_OPTION( EXPORTING IS_SELECT_OPTION = LS_FILTER
                                            IMPORTING ET_SELECT_OPTION = LR_CARRID ).
        WHEN 'CONNID'.
          LO_FILTER->CONVERT_SELECT_OPTION( EXPORTING IS_SELECT_OPTION = LS_FILTER
                                            IMPORTING ET_SELECT_OPTION = LR_CONNID ).
        WHEN 'FLDATE'.
          LO_FILTER->CONVERT_SELECT_OPTION( EXPORTING IS_SELECT_OPTION = LS_FILTER
                                            IMPORTING ET_SELECT_OPTION = LR_FLDATE ).
      ENDCASE.
    ENDLOOP.

"호출 종류 확인"
    IF IO_TECH_REQUEST_CONTEXT->HAS_COUNT( ). "Entity의 전체 건수만 확인
      SELECT COUNT( * ) INTO @SY-DBCNT FROM SFLIGHT
       WHERE CARRID IN @LR_CARRID
         AND CONNID IN @LR_CONNID
         AND FLDATE IN @LR_FLDATE.
      ES_RESPONSE_CONTEXT-COUNT = SY-DBCNT.
    ELSE. "Entity의 Paging된 data 확인
      SELECT * UP TO @LV_TOP ROWS "현재 PAGE에 맞는 최대 조회 건수
        INTO CORRESPONDING FIELDS OF TABLE @LT_ENTITYSET
        FROM SFLIGHT
       WHERE CARRID IN @LR_CARRID
         AND CONNID IN @LR_CONNID
         AND FLDATE IN @LR_FLDATE
       ORDER BY CARRID, CONNID, FLDATE.

      "현재 PAGE에 맞는 데이터만 반환(paging)
      LOOP AT LT_ENTITYSET INTO LS_ENTITY FROM IS_PAGING-SKIP + 1.
        APPEND LS_ENTITY TO ET_ENTITYSET.
        CLEAR LS_ENTITY.
      ENDLOOP.
    ENDIF.

  ENDMETHOD.
```  
</div>
</details>

<details>
<summary><mark>SFLIGHTENTITYSET_UPDATE_ENTITY</mark> 1개의 Entry를 전달받아 Key값 기반으로 기존 데이터를 수정하는 Method. </summary>
<div markdown="1">
```abap
  METHOD SFLIGHTENTITYSET_UPDATE_ENTITY.
    DATA : LS_REQUEST_INPUT_DATA TYPE YCL_YGWSFLIGHT2_MPC_EXT=>TS_SFLIGHTENTITY.
    DATA : LS_DATA TYPE SFLIGHT.

    "전달받은 수정 데이터 추출
    IO_DATA_PROVIDER->READ_ENTRY_DATA( IMPORTING ES_DATA = LS_REQUEST_INPUT_DATA ).

    "수정 데이터 구조로 변경
    LS_DATA = CORRESPONDING #( LS_REQUEST_INPUT_DATA ).

    "데이터 수정
    MODIFY SFLIGHT FROM LS_DATA.

    "수정 성공 데이터 반환
    ER_ENTITY = CORRESPONDING #( LS_DATA ).

  ENDMETHOD.
```  
</div>
</details>

---


## 서비스 테스트 <mark>/N/IWFND/GW_CLIENT</mark>
<img width="1265" height="328" alt="image" src="https://github.com/user-attachments/assets/2a3f3bf8-7514-49a5-9381-ae82b273b8ae" />

<img width="1652" height="832" alt="image" src="https://github.com/user-attachments/assets/0939fd0b-ed63-4b48-b6c4-25d9a90064fb" />  
해당 Entity Set에 속하는 데이터를 확인할 수 있다.
/sap/opu/odata/sap/YGWSFLIGHT4_SRV/SflightEntitySet(Carrid='AA',Connid='0017',Fldate=datetime'2024-11-25T00%3A00%3A00')  
-> 이처럼 조건을 줄 수도 있다.  

<img width="1202" height="526" alt="image" src="https://github.com/user-attachments/assets/03267044-5b9e-4815-a702-58c213c61016" />  
Add URI Option 버튼 클릭하여 $metadata 선택 후 Execute 버튼 클릭 > Odata model의 Entity Type, property 등을 확인 가능하다.  


/sap/opu/odata/sap/YGWSFLIGHT4_SRV/ 으로 실행하였을 때 나오는 xml:base="http://주소/sap/opu/odata/sap/YGWSFLIGHT4_SRV/"는  
BAS Project 생성 시 OData service URL에 입력해야 하므로 복사해둔다.  

---

## BAS에서 Report 생성하여 실행
BAS를 실행하여 F1 > Fiori: Open Application Generator 로 새 프로젝트를 열어준다.  
Template Selection : Basic  

<img width="1299" height="460" alt="image" src="https://github.com/user-attachments/assets/660ec3ef-3af7-4729-b8f9-b1eae071491d" />
-> http://주소/sap/opu/odata/sap/YGWSFLIGHT4_SRV/ 입력할 것.  

Entity Selection ] View Name : List  

Project Attributes ] 이름, 타이틀은 원하는 대로 변경하여 finish.



### List.view 로직 추가
path는 entityset(예:/YDESD_SFLIGHT99)으로 입력한다. 

```xml

```  

### i18n 로직 추가
<img width="731" height="400" alt="image" src="https://github.com/user-attachments/assets/66c2caa6-6418-41ca-95fd-79fb1a37b74d" />  


### 결과
*terminal - 해당 프로젝트 경로에서(cd project1) npm install / npm run start*  
<img width="1902" height="861" alt="image" src="https://github.com/user-attachments/assets/c0294d96-6972-4fa7-bce4-1142a8475293" />  

>SAP 서버의 SFLIGHT 데이터를 UI5로 구현한 화면이 조회된다.
